import com.moowork.gradle.node.task.NodeTask

plugins {
	id 'base'
	id 'com.github.node-gradle.node' version '2.2.4'
}

node {
	version = '12.18.0'
	npmVersion = '6.14.5'
	download = true
}

task npmAuditFix(type: NpmTask, dependsOn: 'npmInstall') {
	args = ['audit', 'fix', '--force']
}

task npmUpdate(type: NpmTask, dependsOn: 'npmInstall') {
	args = ['update', '--save']
}

task vueCliBuild(type: NodeTask, dependsOn: 'npmInstall') {
	inputs.files fileTree("config")
	inputs.files fileTree("public")
	inputs.files fileTree("src")
	inputs.files fileTree("test")
	inputs.files fileTree("webpack")
	inputs.file 'babel.config.js'
	inputs.file 'package.json'
	inputs.file 'package-lock.json'
	inputs.file 'postcss.config.js'
	inputs.file 'vue.config.js'
	outputs.dir 'build'
	def osName = System.getProperty('os.name').toLowerCase();
	if (osName.contains('windows')) {
		script = project.file('node_modules/@vue/cli-service/bin/vue-cli-service.js')
	} else {
		script = project.file('node_modules/.bin/vue-cli-service')
	}
	args = ['build']
}

task vueCliServe(type: NodeTask, dependsOn: 'npmInstall') {
	def osName = System.getProperty('os.name').toLowerCase();
	if (osName.contains('windows')) {
		script = project.file('node_modules/@vue/cli-service/bin/vue-cli-service.js')
	} else {
		script = project.file('node_modules/.bin/vue-cli-service')
	}
	args = ['serve']
}

task packageNpmApp(type: Zip) {
	dependsOn vueCliBuild
	baseName 'viperpit-web'
	extension 'jar'
	from('dist') {
		into 'static' 
	}
}

configurations {
	npmResources
}

artifacts {
	npmResources(packageNpmApp.archivePath) {
		builtBy packageNpmApp
		type 'jar'
	}
}

configurations.default.extendsFrom(configurations.npmResources)

assemble.dependsOn packageNpmApp

clean.delete << file(packageNpmApp.archivePath)
clean.delete << file('node_modules')
clean.delete << file('dist')