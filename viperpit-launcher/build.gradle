plugins {
	id 'java'
	id 'org.springframework.boot'
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

springBoot {
	mainClass = 'de.viperpit.launcher.LauncherApplication'
}

bootJar {
	manifest {
		attributes 'Automatic-Module-Name': 'de.viperpit.launcher'
	}
}

dependencies {
	implementation project(':viperpit-agent')
	implementation enforcedPlatform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
	implementation 'org.slf4j:slf4j-api'
	implementation 'ch.qos.logback:logback-classic'
	implementation 'org.springframework.boot:spring-boot'
	implementation 'org.springframework.boot:spring-boot-autoconfigure'
	implementation 'org.springframework.boot:spring-boot-loader'
}


task runJpackage(type: Exec) {
    dependsOn bootJar
	def javaHome = System.properties['java.home']
	if (javaHome == null) {
		throw new RuntimeException("javaHome is not defined.")
	}
	def name = 'Viperpit Agent'
	def description = 'Serves a touch-enabled web interface for Falcon BMS.'
	def vendor = 'Viper Pits Devs'
	def resourcesDirectory = project.sourceSets.main.resources.srcDirs.first()
	commandLine "$javaHome/bin/jpackage",
				'--resource-dir', resourcesDirectory,
				'--input', "$project.buildDir/libs",
				'--dest', "$project.buildDir/jpackage",
				'--name', name,
				'--description', description,
				'--vendor', vendor,
				'--java-options', '-Djava.library.path=$ROOTDIR;$ROOTDIR/app;C:\\Windows\\System32',
				'--icon', "$resourcesDirectory/icon.ico",
				'--app-version', project.version,
				'--main-class', "org.springframework.boot.loader.launch.JarLauncher",
				'--main-jar', bootJar.archiveFileName.get(),
				'--win-console',
				'--win-dir-chooser',
				'--win-per-user-install'
}

build.dependsOn runJpackage