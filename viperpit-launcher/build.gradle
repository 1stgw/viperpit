plugins {
	id 'java'
	id 'application'
	id 'org.springframework.boot'
	id 'io.spring.dependency-management'
}

sourceCompatibility = JavaVersion.VERSION_15
targetCompatibility = JavaVersion.VERSION_15

tasks.jar {
	manifest {
		attributes('Automatic-Module-Name': 'de.viperpit.launcher')
	}
}

dependencies {
	implementation project(':viperpit-agent')
	implementation 'org.slf4j:slf4j-api'
	implementation 'ch.qos.logback:logback-classic'
	implementation 'org.springframework.boot:spring-boot'
	implementation 'org.springframework.boot:spring-boot-autoconfigure'
}

mainClassName = 'de.viperpit.launcher.LauncherApplication'

task assembleJpackageLauncher(type: Copy) {
	dependsOn jar.archiveFile
	from jar.archiveFile into "${project.buildDir}/jpackage-libs"
}

task assembleJpackageDependencies(type: Copy) {
	dependsOn configurations.runtimeClasspath
	from configurations.runtimeClasspath into "${project.buildDir}/jpackage-libs"
}

task runJpackage(type: Exec) {
	dependsOn assembleJpackageLauncher
	dependsOn assembleJpackageDependencies
	def javaHome = System.properties['java.home']
	if (javaHome == null) {
		throw new RuntimeException("javaHome is not defined.")
	}
	def name = 'Viperpit Agent'
	def description = 'Serves a touch-enabled web interface for Falcon BMS.'
	def vendor = 'Viper Pits Devs'
	def resourcesDirectory = project.sourceSets.main.resources.srcDirs.first()
	commandLine "$javaHome/bin/jpackage",
				'--resource-dir', "$resourcesDirectory",
				'--input', "$project.buildDir/jpackage-libs",
				'--dest', "$project.buildDir/jpackage",
				'--name', "$name",
				'--description', "$description",
				'--vendor', "$vendor",
				'--java-options', '-Djava.library.path=$ROOTDIR;$ROOTDIR/app;C:\\Windows\\System32',
				'--icon', "$resourcesDirectory/icon.ico",
				'--app-version', "$project.version",
				'--main-class', "$project.mainClassName",
				'--main-jar', "$jar.archiveName",
				'--win-console',
				'--win-dir-chooser',
				'--win-per-user-install'
}

build.dependsOn runJpackage